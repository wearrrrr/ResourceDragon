set(LUA_LINE_EDITOR "Autodetect" CACHE STRING "Line editor for interactive editing (Readline, Editline, None or Autodetect)")
set_property(CACHE LUA_LINE_EDITOR PROPERTY STRINGS "Readline" "Editline" "None" "Autodetect")

if (LUA_LINE_EDITOR STREQUAL "Autodetect" OR LUA_LINE_EDITOR STREQUAL "Readline")
    find_package(Readline)
endif ()
if (LUA_LINE_EDITOR STREQUAL "Autodetect" OR LUA_LINE_EDITOR STREQUAL "Editline")
    find_package(Editline)
endif ()

set(LUA_READLINE_SUPPORT OFF)
set(LUA_EDITLINE_SUPPORT OFF)
set(LUA_LINE_EDITING_SUPPORT "None")
set(LUA_USE_READLINE FALSE)

add_library(lua_readline INTERFACE)
if (LUA_LINE_EDITOR STREQUAL "Readline" AND Readline_FOUND)
    set(LUA_READLINE_SUPPORT ON)
elseif (LUA_LINE_EDITOR STREQUAL "Editline" AND Editline_FOUND)
    set(LUA_EDITLINE_SUPPORT ON)
elseif (LUA_LINE_EDITOR STREQUAL "Autodetect")
    if (Readline_FOUND)
        set(LUA_READLINE_SUPPORT ON)
    elseif (Editline_FOUND)
        set(LUA_EDITLINE_SUPPORT ON)
    endif ()
endif ()

if (LUA_READLINE_SUPPORT)
    set(LUA_LINE_EDITING_SUPPORT "GNU Readline")
    set(LUA_USE_READLINE TRUE)
    target_link_libraries(lua_readline INTERFACE Readline::Readline)
elseif (LUA_EDITLINE_SUPPORT)
    set(LUA_LINE_EDITING_SUPPORT "BSD Editline")
    set(LUA_USE_READLINE TRUE)
    target_link_libraries(lua_readline INTERFACE Editline::Editline)
endif ()
